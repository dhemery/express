buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.0")
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

ext {
    vendor = [name: 'Dale H. Emery', group: 'com.dhemery']
    stylesheet = new File(projectDir, "/stylesheets/stylesheet.css")
}

project.description = 'Expression Kit'
project.group = vendor.group
project.version = "1.0.0"

project.dependencies {
    compile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.jmock:jmock-junit4:2.8.1'
    testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0")
//    testRuntime("org.junit.vintage:junit-vintage-engine:4.12.0")
}

junitPlatform {
    details 'tree'
}

junitPlatformTest {
// Force tests to run even when up to date
    outputs.upToDateWhen { false }
}

task sourcesJar(type: Jar) {
    description = 'Assembles a jar containing the main sources.'
    classifier 'sources'
    from sourceSets.main.allSource
}

tasks.javadoc {
    inputs.file project.stylesheet
    options.stylesheetFile = project.stylesheet
    options.tags 'implSpec:a:Implementation Requirements'
    options.tags 'implNote:a:Implementation Note'
    options.links "http://docs.oracle.com/javase/8/docs/api/"
    options.links "http://hamcrest.org/JavaHamcrest/javadoc/1.3/"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Assembles a jar containing the javadoc documentation.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.install.dependsOn clean, test

sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9

tasks.jar {
    manifest {
        attributes(
                "Implementation-Title": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor": vendor.name
        )
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

repositories {
    mavenLocal()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
