buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.junit.platform:junit-platform-gradle-plugin:1.0.1")
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    vendor = [name: 'Dale H. Emery', group: 'com.dhemery']
    stylesheet = new File(projectDir, "/stylesheets/stylesheet.css")
    junitPlatformVersion = '1.0.1'
    junitJupiterVersion  = '5.0.1'
}

description = 'Expression Kit'
group = vendor.group
version = "1.0.0"

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'


dependencies {
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitJupiterVersion
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitJupiterVersion

    testRuntime group: 'org.junit.platform', name: 'junit-platform-launcher', version: junitPlatformVersion

    testCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: '1.0.0'
}

junitPlatform {
    details 'tree'
}

task sourcesJar(type: Jar) {
    description = 'Assembles a jar containing the main sources.'
    classifier 'sources'
    from sourceSets.main.allSource
}

tasks.javadoc {
    inputs.file project.stylesheet
    options.stylesheetFile = project.stylesheet
    options.tags 'implSpec:a:Implementation Requirements'
    options.tags 'implNote:a:Implementation Note'
    options.links "http://docs.oracle.com/javase/9/docs/api/"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Assembles a jar containing the javadoc documentation.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.install.dependsOn clean, test

sourceCompatibility = JavaVersion.VERSION_1_9
targetCompatibility = JavaVersion.VERSION_1_9

tasks.jar {
    manifest {
        attributes(
                "Implementation-Title": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor": vendor.name
        )
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
