import org.gradle.api.JavaVersion

apply plugin: 'java'
apply plugin: 'maven'

ext {
    vendor = [name: 'Dale H. Emery', group: 'com.dhemery']
    stylesheet = new File(projectDir, "/stylesheets/stylesheet.css")
}

project.description = 'Expression Kit'
project.group = vendor.group
project.version = "1.0.0"

project.dependencies {
    compile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.jmock:jmock-junit4:2.8.1'
}

task sourcesJar(type: Jar) {
    description = 'Assembles a jar containing the main sources.'
    classifier 'sources'
    from sourceSets.main.allSource
}

tasks.javadoc {
    inputs.file project.stylesheet
    options.stylesheetFile = project.stylesheet
    options.tags 'implSpec:a:Implementation Requirements'
    options.tags 'implNote:a:Implementation Note'
    options.links "http://docs.oracle.com/javase/8/docs/api/"
    options.links "http://hamcrest.org/JavaHamcrest/javadoc/1.3/"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Assembles a jar containing the javadoc documentation.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.install.dependsOn clean, test

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.jar {
    manifest {
        attributes(
                "Implementation-Title": project.description,
                "Implementation-Version": project.version,
                "Implementation-Vendor": vendor.name
        )
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

repositories {
    mavenLocal()
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}